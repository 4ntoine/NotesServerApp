ext {
    h2_version = '1.4.199'
    ormlite_version = '4.45'
    grpc_version = '1.21.0'
    module_version = '1.1'
    micronaut_version = '1.2.0'
}

buildscript {
    ext.kotlin_version = '1.3.41'
    ext.spring_boot_version = '2.1.3.RELEASE'

    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.7"
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.8'
    }
}

def isMultiplatform(def moduleName) {
    return moduleName == 'domain' || moduleName == 'app-api'
}

subprojects {
    apply plugin: 'idea'

    if (!isMultiplatform(it.name)) {
        apply plugin: 'kotlin'
    }

    apply plugin: 'kotlin-spring'

    apply plugin: 'maven-publish'

    group 'name.antonsmirnov.notes'
    version "$rootProject.module_version"

    if (!isMultiplatform(it.name)) {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                }
            }
        }

        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    repositories {
        jcenter()
        mavenCentral()
        maven { url "https://jcenter.bintray.com" }
    }

    if (!isMultiplatform(it.name)) {
        // common submodules dependencies
        dependencies {
            implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            testImplementation 'org.jetbrains.kotlin:kotlin-test'
            testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
        }
    }
}
